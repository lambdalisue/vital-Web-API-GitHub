let s:V = vital#of('vital')
let s:J = s:V.import('Web.JSON')
let s:G = s:V.import('Web.API.GitHub')
let s:P = s:V.import('System.Filepath')
let s:S = s:V.import('Vim.ScriptLocal')
let s:sv = s:S.svars(s:P.realpath(s:P.abspath(
      \ 'autoload/vital/__latest__/Web/API/GitHub.vim',
      \)))

Describe Web.API.GitHub
  Context .get_config()
    It returns a config dictionary
      let ret = s:G.get_config()
      Assert IsDict(ret)
      Assert KeyExists(ret, 'baseurl')
      Assert KeyExists(ret, 'authorize_scopes')
      Assert KeyExists(ret, 'authorize_note')
      Assert KeyExists(ret, 'authorize_note_url')
    End
  End

  Context .set_config({config})
    It overrides a config dictionary
      let old_config = s:G.get_config()
      let new_config = {
            \ 'baseurl': 'foobar',
            \ 'authorize_scopes': ['foo'],
            \ 'authorize_note': 'hello',
            \ 'authorize_note_url': 'hoge',
            \}
      try
        call s:G.set_config(new_config)
        let ret = s:G.get_config()
        Assert Equals(ret.baseurl, 'foobar')
        Assert Equals(ret.authorize_scopes, ['foo'])
        Assert Equals(ret.authorize_note, 'hello')
        Assert Equals(ret.authorize_note_url, 'hoge')
      finally
        call s:G.set_config(old_config)
      endtry
    End
  End

  Context .new({options})
    It returns a client object
      let ret = s:G.new()
      Assert IsDict(ret)
      Assert KeyExists(ret, 'baseurl')
      Assert KeyExists(ret, 'authorize_scopes')
      Assert KeyExists(ret, 'authorize_note')
      Assert KeyExists(ret, 'authorize_note_url')
      " Public methods
      Assert KeyExists(ret, 'login')
      Assert KeyExists(ret, 'logout')
      Assert KeyExists(ret, 'request')
      Assert KeyExists(ret, 'get')
      Assert KeyExists(ret, 'post')
      Assert KeyExists(ret, 'put')
      Assert KeyExists(ret, 'patch')
      Assert KeyExists(ret, 'delete')
    End
  End

  Context A client instance
    Before
      let client = s:G.new()
      function! s:sv.H.request(...) abort
        let self._request_args = a:000
        return {
              \ 'status': 200,
              \ 'statusText': 'success',
              \ 'content': '',
              \}
      endfunction
    End

    Context .authorize({username}[, {options}])
      It returns a new personal access toekn
        Assert KeyExists(client, 'authorize')
        Skip The method uses interactive functions
      End
    End

    Context .authorize_with_password({username}, {password}[, {otp}])
      It request a personal access token to GitHub API
        let ret = client.authorize_with_password(
              \ 'username',
              \ 'password',
              \)
        Assert Equals(ret, {
              \ 'status': 200,
              \ 'statusText': 'success',
              \ 'content': '',
              \})
        let args = s:sv.H._request_args
        let data = s:J.encode({
              \ 'scopes': [],
              \ 'note': client.get_authorize_note(),
              \ 'note_url': '',
              \})
        Assert Equals(args, [{
              \ 'headers': {},
              \ 'method': 'POST',
              \ 'data': data,
              \ 'url': 'https://api.github.com/authorizations',
              \ 'anonymous': 1,
              \ 'username': 'username',
              \ 'password': 'password',
              \ 'authMethod': 'basic',
              \}])
      End

      It request a personal access token to GitHub API with {otp}
        let ret = client.authorize_with_password(
              \ 'username',
              \ 'password',
              \ '012345',
              \)
        Assert Equals(ret, {
              \ 'status': 200,
              \ 'statusText': 'success',
              \ 'content': '',
              \})
        let args = s:sv.H._request_args
        let data = s:J.encode({
              \ 'scopes': [],
              \ 'note': client.get_authorize_note(),
              \ 'note_url': '',
              \})
        Assert Equals(args, [{
              \ 'headers': {
              \   'X-GitHub-OTP': '012345',
              \ },
              \ 'method': 'POST',
              \ 'data': data,
              \ 'url': 'https://api.github.com/authorizations',
              \ 'anonymous': 1,
              \ 'username': 'username',
              \ 'password': 'password',
              \ 'authMethod': 'basic',
              \}])
      End
    End

    Context .authenticate({username}, {token}[, {options})
      It confirm an authentication of {username} with {token}
        let ret = client.authenticate(
              \ 'username',
              \ '012345', {
              \   'verbose': 0,
              \ }
              \)
        let args = s:sv.H._request_args
        Assert Equals(ret, 1)
        Assert Equals(args, [{
              \ 'headers': {
              \   'Authorization': 'token 012345',
              \ },
              \ 'param': {},
              \ 'method': 'GET',
              \ 'url': 'https://api.github.com/user',
              \ 'anonymous': 0,
              \ 'token': '012345',
              \}])
      End
    End
  End
End
